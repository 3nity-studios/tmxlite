@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

# Choose which target definitions must be imported
if(TMXLITE_STATIC_LIBRARIES)
    set(config_name "Static")
else()
    set(config_name "Shared")
endif()
set(targets_config_file "${CMAKE_CURRENT_LIST_DIR}/tmxlite${config_name}Targets.cmake")

# Generate imported targets for TMXLITE and its dependencies
if(EXISTS "${targets_config_file}")
    # Set TMXLITE_FOUND to ON by default, may be overwritten by one of the includes below
    set(TMXLITE_FOUND ON)
    include("${targets_config_file}")
    include("${CMAKE_CURRENT_LIST_DIR}/tmxliteConfigDependencies.cmake")

    if(TMXLITE_FOUND)
        set(TMXLITE_VERSION_IS_RELEASE @VERSION_IS_RELEASE@)
    endif()
else()
    set(FIND_TMXLITE_ERROR "Requested TMXLITE configuration (${config_name}) was not found")
    set(TMXLITE_FOUND OFF)
endif()

if(NOT TMXLITE_FOUND)
    if(TMXLITE_FIND_REQUIRED)
        # fatal error
        message(FATAL_ERROR "${FIND_TMXLITE_ERROR}")
    elseif(NOT TMXLITE_FIND_QUIETLY)
        # error but continue
        message(STATUS "${FIND_TMXLITE_ERROR}")
    endif()
endif()

if(TMXLITE_FOUND AND NOT TMXLITE_FIND_QUIETLY)
    message(STATUS "Found TMXLITE @PROJECT_VERSION@ in ${CMAKE_CURRENT_LIST_DIR}")
endif()
